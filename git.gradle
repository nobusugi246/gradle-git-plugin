apply plugin: JGitPlugin

git {
  repository = '.'
  filepattern = ['']

  user_name = ''
  user_email = ''

  remote   = ''
  upstream = ''
  account = ''
  password = ''

  middle = ''
  middleAccount = ''
  middlePassword = ''

  comment = ''
}


githubFlow {
  topic = ''
}


// ======== ======== ======== ======== ======== ======== ======== ========
// -------- -------- -------- -------- -------- -------- -------- --------
// https://github.com/centic9/jgit-cookbook/tree/master/src/main/java/org/dstadler/jgit
// http://download.eclipse.org/jgit/site/4.1.1.201511131810-r/apidocs/index.html

buildscript {
  ext { jgitVersion = '4.1.1.201511131810-r' }
  repositories { mavenCentral() }
  dependencies {
    classpath "org.eclipse.jgit:org.eclipse.jgit:${jgitVersion}"
    classpath "org.eclipse.jgit:org.eclipse.jgit.ui:${jgitVersion}"
    classpath "org.eclipse.jgit:org.eclipse.jgit.pgm:${jgitVersion}"
  }
}

import java.text.SimpleDateFormat
import java.util.concurrent.CountDownLatch
import org.eclipse.jgit.api.Git
import org.eclipse.jgit.api.ListBranchCommand.ListMode
import org.eclipse.jgit.lib.ObjectId
import org.eclipse.jgit.lib.Repository
import org.eclipse.jgit.lib.RepositoryBuilder
import org.eclipse.jgit.pgm.Main
import org.eclipse.jgit.storage.file.FileRepositoryBuilder
import org.eclipse.jgit.transport.TagOpt
import org.eclipse.jgit.transport.UsernamePasswordCredentialsProvider

class JGitPlugin implements Plugin<Project> {
  void apply(Project project) {
    // creates DSL of git repositories.
    project.extensions.create("git", JGitPluginExtension)
    // creates DSL of GitHub Flow.
    project.extensions.create("githubFlow", JGitHubFlowPluginExtension)

    // initializes current folder.
    project.task('initGit') << {
      Git.init()
         .setDirectory(new File(project.git.repository))
         .setBare(false)
         .call()

      project.tasks.setConfig.execute()
    }
    project.tasks.initGit.description = 'Initializes current folder as a local repository.'

    // Set configuration to local repository.
    project.task('setConfig') << {
      def git = getGitInstance(project.git.repository)
      def config = git.getRepository().getConfig()
      config.setString('user', null, 'name', project.git.user_name)
      config.setString('user', null, 'email', project.git.user_email)
      config.setString('remote', 'origin', 'url', project.git.remote)
      config.setString('remote', 'origin', 'fetch', '+refs/heads/*:refs/remotes/origin/*')
      config.setString('remote', 'upstream', 'url', project.git.upstream)
      config.setString('remote', 'upstream', 'fetch', '+refs/heads/*:refs/remotes/upstream/*')
      config.setString('remote', 'middle', 'url', project.git.middle)
      config.setString('remote', 'middle', 'fetch', '+refs/heads/*:refs/remotes/middle/*')
      config.save()
    }
    project.tasks.setConfig.description = 'Set configuration to local repository.'

    // Creates a topic branch and checkouts it.
    project.task('branchCreateAndCheckout') << {
      def git = getGitInstance(project.git.repository)
      git.branchCreate()
         .setName(project.githubFlow.topic)
         .call()

      git.checkout()
         .setName(project.githubFlow.topic)
         .call()
    }
    project.tasks.branchCreateAndCheckout.description = 'Creates a topic branch and checkouts it.'

    // Add files to current repository.
    project.task('addFilePattern') << {
      def git = getGitInstance(project.git.repository)
      project.git.filepattern.each { fp ->
        git.add()
           .addFilepattern(fp)
           .call()
      }
    }
    project.tasks.addFilePattern.description = 'Add files to current repository.'

    // Commits to local repository.
    project.task('commit') << {
      def git = getGitInstance(project.git.repository)
      git.commit()
         .setAll(true)
         .setMessage(project.git.comment)
         .call()
    }
    project.tasks.commit.description = 'Commits to local repository.'

    // Pushes to remote repository.
    project.task('push') << {
      def git = getGitInstance(project.git.repository)
      git.push()
         .setPushAll()
         .setCredentialsProvider(new UsernamePasswordCredentialsProvider(project.git.account, project.git.password))
         .call()
    }
    project.tasks.push.description = 'Pushes to remote repository.'

    // initialize current folder, add files, commit them, then push to remote repository.
    project.task('initThenPush') << {
      project.tasks.initGit.execute()
      project.tasks.addFilePattern.execute()
      project.tasks.commit.execute()
      project.tasks.push.execute()
    }
    project.tasks.initThenPush.description = 'Initializes local repository then pushes to remote repository.'

    // Pushes to middle server.
    project.task('middlePush') << {
      println "Push to ${project.git.middle}"
      def git = getGitInstance(project.git.repository)
      git.push()
         .setRemote(project.git.middle)
         .setPushAll()
         .setCredentialsProvider(new UsernamePasswordCredentialsProvider(project.git.middleAccount,
                                                                         project.git.middlePassword))
         .call()
    }
    project.tasks.middlePush.description = 'Pushes to middle server.'

    // Fetches from middle server and merge it.
    project.task('middleFetchAndMerge') << {
      println "Fetch from ${project.git.middle}"
      def git = getGitInstance(project.git.repository)
      def result = git.fetch()
                      .setRemote('middle')
                      .setCheckFetchedObjects(true)
                      .setTagOpt(TagOpt.AUTO_FOLLOW)
                      .call()
      println "Result: ${result.getMessages()}"

      println ''
      println 'Merge middle/master'
      def mergeResult = git.merge()
                           .include(git.getRepository().getRef('middle/master'))
                           .call()
      println "Result: ${mergeResult}"
    }
    project.tasks.middleFetchAndMerge.description = 'Fetches from middle server and merge it.'

    // Clones remote repository.
    project.task('clone') << {
      println "Clone from ${project.git.remote}"
      def result = Git.cloneRepository()
                      .setURI(project.git.remote)
                      .setDirectory(new File(project.git.repository))
                      .call()
      println "Result: ${result}"
      result.getRepository().close()
      project.tasks.setConfig.execute()
    }
    project.tasks.clone.description = 'Clones remote repository.'

    // Clones upstream repository.
    project.task('cloneUpstream') << {
      println "Clone from ${project.git.upstream}"
      def result = Git.cloneRepository()
                      .setURI(project.git.upstream)
                      .setDirectory(new File(project.git.repository))
                      .call()
      println "Result: ${result}"
      result.getRepository().close()
      project.tasks.setConfig.execute()
    }
    project.tasks.cloneUpstream.description = 'Clones upstream repository.'

    // Fetches remote repository.
    project.task('fetch') << {
      println "Fetch from ${project.git.remote}"
      def git = getGitInstance(project.git.repository)
      def result = git.fetch()
                      .setCheckFetchedObjects(true)
                      .setTagOpt(TagOpt.AUTO_FOLLOW)
                      .call()
      println "Result: ${result.getMessages()}"
    }
    project.tasks.fetch.description = 'Fetches remote repository.'

    // sync a fork.
    // https://help.github.com/articles/syncing-a-fork/
    project.task('fetchUpstreamAndMerge') << {
      println "Fetch from ${project.git.upstream}"
      def git = getGitInstance(project.git.repository)
      def fetchResult = git.fetch()
                           .setRemote('upstream')
                           .setCheckFetchedObjects(true)
                           .setTagOpt(TagOpt.AUTO_FOLLOW)
                           .call()
      println "Result: ${fetchResult.getMessages()}"
      
      println ''
      println 'Merge upstream/master'
      def mergeResult = git.merge()
                           .include(git.getRepository().getRef('upstream/master'))
                           .call()
      println "Result: ${mergeResult}"
    }
    project.tasks.fetchUpstreamAndMerge.description = 'Fetches from Original Repository and merge it.'

    project.task('sync') << {
      project.tasks.fetchUpstreamAndMerge.execute()
    }
    project.tasks.sync.description = 'Sync a fork.'
    
    // display tags.
    project.task('tag') << {
      def git = getGitInstance(project.git.repository)
      git.tagList().call().each {
        println "tag: ${it.getName()}"
      }
    }
    project.tasks.tag.description = 'Displays tags.'

    // display text log.
    project.task('log') << {
      def git = getGitInstance(project.git.repository)
      def fmt = new SimpleDateFormat("yyyy-MM-dd HH:mm:ss");
      git.log().all().call().each {
        println "${fmt.format(it.getCommitterIdent().getWhen())}  ${it.getShortMessage()}"
      }
    }
    project.tasks.log.description = 'Displays text log.'

    // display graphical log of local git repository.
    project.task('glog') << {
      def latch = new CountDownLatch(1)
      def main = new Main()
      main.execute('glog')
      latch.await()
    }
    project.tasks.glog.description = 'Displays graphical log.'

    // display existing branches.
    project.task('branch') << {
      def git = getGitInstance(project.git.repository)
      git.branchList().setListMode(ListMode.ALL).call().each {
        println "Branch: ${it.getName()}"
      }
    }
    project.tasks.branch.description = 'Displays branch(es).'

    // display status of local repository.
    project.task('status') << {
      def git = getGitInstance(project.git.repository)
      def status = git.status().call();
      println "Added: " + status.getAdded()
      println "Changed: " + status.getChanged()
      println "Conflicting: " + status.getConflicting()
      println "ConflictingStageState: " + status.getConflictingStageState()
      println "IgnoredNotInIndex: " + status.getIgnoredNotInIndex()
      println "Missing: " + status.getMissing()
      println "Modified: " + status.getModified()
      println "Removed: " + status.getRemoved()
      println "Untracked: " + status.getUntracked().size()
      println "UntrackedFolders: " + status.getUntrackedFolders()
    }
    project.tasks.status.description = 'Displays status.'
  }

  // get Git instance of current folder.
  def getGitInstance(String folder) {
    def repository = new FileRepositoryBuilder().create(new File("$folder/.git"))
    return new Git(repository)
  }
}

// DSL Class of git repositories.
class JGitPluginExtension {
  String repository
  String comment
  String remote
  String upstream
  String user_name
  String user_email
  String account
  String password
  String middle
  String middleAccount
  String middlePassword
  ArrayList<String> filepattern
}

// DSL Class of GitHub Flow.
class JGitHubFlowPluginExtension {
  String topic
}
